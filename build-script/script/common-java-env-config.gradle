apply plugin: "io.spring.dependency-management"

def obtainJavaVersion() {
    springBoot3Modules.contains(project.name) ? maximumJavaVersion : defaultJavaVersion
}

def obtainSpringBootBomVersion() {
    springBoot3Modules.contains(project.name) ? maximumSpringBootBomVersion : defaultSpringBootBomVersion
}

def obtainSpringCloudBomVersion() {
    springBoot3Modules.contains(project.name) ? maximumSpringCloudBomVersion : defaultSpringCloudBomVersion
}

dependencyManagement {
    resolutionStrategy {
        cacheChangingModulesFor 0, "seconds"
    }
    applyMavenExclusions = false
    generatedPomCustomization {
        enabled = false
    }
    imports {
        mavenBom "org.springframework.boot:spring-boot-dependencies:${obtainSpringBootBomVersion()}"
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${obtainSpringCloudBomVersion()}"
    }
    dependencies {
        dependency "org.hibernate:hibernate-validator:8.0.0.Final"
        dependency("io.github.hylexus.oaks:oaks-common-utils:1.0.7") {
            exclude "org.projectlombok:lombok"
        }
        dependency "com.github.sarveswaran-m:util.concurrent.blockingMap:0.91"
        dependency "com.google.guava:guava:31.1-jre"
        dependency "com.google.code.findbugs:jsr305:3.0.2"
        dependency "com.google.code.findbugs:annotations:3.0.1"
        dependency "javax.annotation:javax.annotation-api:1.3.2"
        dependency "org.apache.commons:commons-collections4:4.4"
        dependency "org.bouncycastle:bcprov-jdk18on:1.78.1"
    }
}

apply plugin: 'java-library'

java {
    sourceCompatibility = obtainJavaVersion()
    targetCompatibility = obtainJavaVersion()
}

tasks.withType(JavaCompile) {
    options.encoding = "${defaultCharset}"
}

repositories {
    mavenLocal()
    [
            'https://maven.aliyun.com/repository/central',
            'https://maven.aliyun.com/repository/public',
            'https://maven.aliyun.com/repository/google',
            'https://maven.aliyun.com/repository/spring',
            'https://maven.aliyun.com/repository/spring-plugin',
            'https://maven.aliyun.com/repository/gradle-plugin',
            'https://maven.aliyun.com/repository/grails-core',
            'https://maven.aliyun.com/repository/apache-snapshots',
            "https://plugins.gradle.org/m2/",
            "https://repo.spring.io/release",
            "https://repo.spring.io/snapshot",
    ].each { repoUrl ->
        maven { url repoUrl }
    }
    mavenCentral()
}

dependencies {
    annotationProcessor 'org.projectlombok:lombok'
    compileOnly 'org.projectlombok:lombok'

    // for test-class
    testAnnotationProcessor 'org.projectlombok:lombok'
    testCompileOnly 'org.projectlombok:lombok'
}

compileJava {
    sourceCompatibility = obtainJavaVersion()
    targetCompatibility = obtainJavaVersion()
    options.encoding = "${defaultCharset}"
}

javadoc {
    description = "Generates project-level javadoc for use in -javadoc jar"

    options.encoding = "UTF-8"
    options.memberLevel = JavadocMemberLevel.PROTECTED
    options.author = true
    options.header = project.name
    options.use = true
    options.addStringOption("Xdoclint:none", "-quiet")
    options.charSet = 'UTF-8'
    options.addStringOption("charset", "UTF-8")

    options {
        version true
        links "https://docs.oracle.com/en/java/javase/11/docs/api"
    }
    // Suppress warnings due to cross-module @see and @link references.
    // Note that global 'api' task does display all warnings.
    logging.captureStandardError LogLevel.INFO
    logging.captureStandardOutput LogLevel.INFO  // suppress "## warnings" message
}

apply plugin: "checkstyle"
checkstyle {
    // jdk8版本
    //toolVersion = "8.29"
    // jdk11版本
    toolVersion = "10.9.1"
    getConfigDirectory().set(rootProject.file("build-script/checkstyle/"))
}

apply plugin: 'idea'
idea {
    module {
        inheritOutputDirs = false
        outputDir = compileJava.destinationDirectory.getAsFile().get()
        testOutputDir = compileTestJava.destinationDirectory.getAsFile().get()
    }
}
